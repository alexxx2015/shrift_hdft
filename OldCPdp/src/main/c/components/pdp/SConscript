# components/pdp/SConscript
import os, sys, platform
Import('env','curBaseDir')

authActionObject = env.rdObject('authorizationAction.c')
execActionObject = env.rdObject('executeAction.c')
mechanismObject = env.rdObject('mechanism.c')
env.rdAlias('mechanism', [mechanismObject, authActionObject, execActionObject])

pdpCCFlags=env['CCFLAGS'] + " -DLIBXML_STATIC "
if env['PEFCONFIG']['pdpjni'] == True:
  pdpCCFlags=pdpCCFlags + " -DPDP_JNI "

pdpDependencyLibs =env.getAliasObjects('logger') + env.getAliasObjects('action') + env.getAliasObjects('pefXMLutils') + env.getAliasObjects('pefStringUtils')
pdpDependencyLibs+=env.getAliasObjects('pefThreadUtils') + env.getAliasObjects('pefMemUtils')
pdpDependencyLibs+=env.getAliasObjects('timestamp') + env.getAliasObjects('event')
pdpDependencyLibs+=env.getAliasObjects('condition') + env.getAliasObjects('mechanism') + env.getAliasObjects('pdpInterfaces')

if env['PEFCONFIG']['socketUtils'] == True:
  pdpDependencyLibs+=env.getAliasObjects('pefSocketUtils')
  
if env['PEFCONFIG']['soupUtils'] == True:
  pdpDependencyLibs+=env['pefSoupUtils'] + ['soup-2.4']
  
pdpInternalObject=env.rdObject('pdpInternal.c', ccflags=pdpCCFlags)
pdpDependencyLibs+=pdpInternalObject

if env['PDPCONFIG']['PDP_RMI'] > 0 or env['PEFCONFIG']['pdpjni'] == True:
  pdpDependencyLibs+=env.getAliasObjects('pefJavaUtils')
  
  if env['PDPCONFIG']['PDP_RMI'] > 0:
    # Creating RMI server
    SConscript(os.path.join(env['PEFBASE'], "..", "java", "jpdp", "SConscript"), exports=['env'])
    Default(env.getAliasObjects('rmiClasses'))
    
pdpLog=env.pefLogger('pdp.c')
if sys.platform == "win32" or env['PEFtarget'] == "win32" or env['PEFtarget'] == "win64":
  pdpObject=env.Object('pdp.c', CCFLAGS=pdpCCFlags)
  env.Replace(WINDOWS_INSERT_DEF = True)
  pdpSHLINK=env['SHLINKFLAGS'] + " -DPDP_JNI -DGLIB_STATIC_COMPILATION -DGOBJECT_STATIC_COMPILATION -D_JNI_IMPLEMENTATION_ -DLIBXML_STATIC -Wl,--kill-at -Wl,--export-all-symbols "
  pdp=env.SharedLibrary("pdp.dll", [pdpObject, pdpDependencyLibs],
                        SHLINKFLAGS=pdpSHLINK, SHLIBPREFIX="",
                        LIBS=[env['LibXML2Static'], env['LibZStatic']] + 
                             [env['LibGLibStatic'], env['LibIntlStatic'], env['LibIConvStatic'],env['LibWinPThreadStatic']] +
                             ['ole32','ws2_32','wsock32'] +
                             ['jvm']
                         )
  Depends(pdp, pdpDependencyLibs)
                   
elif env['android']!=True:
  pdpLibs=env['LIBS'] + ['glib-2.0','pthread','xml2']
  if env['PDPCONFIG']['PDP_RMI'] > 0:
    pdpLibs+=['jvm']
  pdp=env.SharedLibrary('pdp.c', CCFLAGS=pdpCCFlags, 
                                 LIBS=pdpDependencyLibs + pdpLibs)
                                 
  
else:
  pdp=pdpDependencyLibs
  Depends(pdp, pdpLog)
  Default(pdp)
  
installTarget=env['PEFtarget']
if installTarget=="native":
  installTarget=str.lower(platform.system()+"-"+platform.machine())

env.Install(os.path.join(env['PEFBUILD'], "..", "..", "target","classes", "nativeLibs", installTarget), pdp)
env.Install(os.path.join(env['PEFBUILD'], "..", "..", "nativeLibs", installTarget), pdp)

Depends(pdp, pdpLog)
Default([pdp, os.path.join(env['PEFBUILD'], "..", "..", "target","classes", "nativeLibs", installTarget), os.path.join(env['PEFBUILD'], "..", "..", "nativeLibs", installTarget)])
env.rdAlias('pdp', [pdp, os.path.join(env['PEFBUILD'], "..", "..", "target","classes", "nativeLibs", installTarget), os.path.join(env['PEFBUILD'], "..", "..", "nativeLibs", installTarget)])




